apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version project.VERSION_NAME
group 'com.radusalagean'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/quickdotsview-release.aar")
            groupId this.group
            artifactId 'quickdotsview'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones),
                // adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.property('bintrayUsername')
    key = project.property('bintrayApiKey')
    publications = ['Production']
    configurations = ['archives']
    // Default: false. Whether to run this as dry-run, without deploying
    dryRun = false
    // Default: false. Whether to override version artifacts already published
    override = false
    // Default: false. Whether version should be auto published after an upload
    publish = false
    pkg {
        repo = 'android-libs'
        name = 'QuickDotsView'
        version {
            name = this.version
            released = new Date()
            vcsTag = this.version
        }
    }
}